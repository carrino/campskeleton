buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath fileTree (dir: 'lib/', includes: ['*.jar'])
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.13.1"
    id 'maven-publish'
    id 'net.nemerosa.versioning' version '2.4.0'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'com.github.ben-manes.versions' version '0.13.0'
    id "com.palantir.flexversion" version "0.9.2"
}

flexversion {
  useTags = true
}

addPrintVersionTask()
version flexVersion()

apply plugin: 'eclipse'
apply plugin: 'java'
apply from: 'gradle/publishing.gradle'

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

// JDK 8 is way too strict about Javadoc style
if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'javax.vecmath', name: 'vecmath', version: '1.5.2'
    compile group: 'gov.nist.math', name: 'jama', version: '1.0.3'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
